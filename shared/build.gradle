apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

def RememberFramework = "RememberShared"

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS') {
            binaries {
                framework(RememberFramework)
            }
        }

        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain.dependencies {
            // kotlin
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
            // kotlin/coroutines
            api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutinesVersion"

            // ktor
            api "io.ktor:ktor-client-core:$ktorVersion"
            api "io.ktor:ktor-client-json:$ktorVersion"
            api "io.ktor:ktor-client-cio:$ktorVersion"

            // serialisation
            api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializationVersion"
        }

        commonTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion"
            implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
            implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
            implementation "io.mockk:mockk-common:$mockkVersion"
            implementation "io.mockk:mockk:$mockkVersion"
        }

        androidMain.dependencies {
            // kotlin
            api 'org.jetbrains.kotlin:kotlin-stdlib'
            // kotlin/coroutines
            api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

            // ktor
            api "io.ktor:ktor-client-android:$ktorVersion"
            api "io.ktor:ktor-client-okhttp:$ktorVersion"
            api "io.ktor:ktor-client-json-jvm:$ktorVersion"

            implementation "org.jsoup:jsoup:$jsoupVersion"

            // serialisation
            api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializationVersion"
        }

        iOSMain {
            dependsOn commonMain
            dependencies {
                // kotlin
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                // kotlin
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutinesVersion"

                // ktor
                implementation "io.ktor:ktor-client-core-native:$ktorVersion"
                implementation "io.ktor:ktor-client-ios:$ktorVersion"

                // serialisation
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serializationVersion"
            }
        }
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Setup Android specific info for androidMain sub-library
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

android {
    compileSdkVersion compileVersion

    defaultConfig {
        minSdkVersion minVersion
        targetSdkVersion targetVersion
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs = ['src/androidMain/kotlin']
            res.srcDirs = ['src/androidMain/res']
        }
    }

    testOptions.unitTests.includeAndroidResources = true
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.iOS.binaries.getFramework(RememberFramework, mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    from { framework.outputFile.parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}
tasks.build.dependsOn packForXCode